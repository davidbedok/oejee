apply plugin: 'eclipse'
apply plugin: 'ear'

version = '1.0'

ext {
	log4jVersion = '1.2.17'
	testngVersion = '6.9.+'
	jeeVersion = '6.0'  
	jbossjee6Version = '3.0.3.Final' 
	mockitoVersion = '1.10.8'
	jbossjmsclientVersion = '7.2.0.Final'
	
	webserviceArchiveName = 'lot-webservice.war'
	webserviceContextPath = 'lottery'
	
	deployLocation = System.getenv('JBOSS_HOME') + '/standalone/deployments/'
}

subprojects {
	apply plugin: 'eclipse'
	apply plugin: 'java'

	repositories { 
		mavenCentral() 
	}

	dependencies {
		compile group: 'log4j', name: 'log4j', version: log4jVersion
		testCompile group: 'org.testng', name: 'testng', version: testngVersion
		testCompile group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
	}
}

ear {
	deploymentDescriptor { 
    	webModule( webserviceArchiveName, webserviceContextPath ) 
    }
}

dependencies {
	deploy project('lot-ejbservice')
	deploy project('lot-persistence')
	deploy project(path: 'lot-webservice', configuration: 'archives')
}

task deployClean ( type: Delete ) {
	delete deployLocation + "${project.name}-${version}.ear"
	sleep(2000)
}

task deployEar ( type: Copy ) {
	dependsOn 'deployClean'
	from "build/libs/${project.name}-${version}.ear"
	into deployLocation
}